urq_mode="dosurq"
:-1
instr jl=Style_DOS_TextColor=15
instr fl=Style_DOS_TextColor=7
#%jl$
pln                      Alice's Adventures in Urqland
#%fl$
;разумеется квест про мутантов, про кого же ещё.
btn London1, Новая игра
btn exit, Выход
end

:exit
end

:London1
pln 	- Алиса.. - зашептал кто-то мне на ухо. - Проснись Алиса.
pln 	Я вскочила с кровати, но рядом никого не было...
btn Glava1_les, Осмотреться
end

:Glava1_les
p 	Я на небольшой поляне. 
if govorila_s_privratnikom=2 and rnd3=1 then p Глубоко из леса доносятся леденящие душу крики моего старого знакомого: "Белки! Надо только подпрыгнуть! И быстро-быстро махать крыльями!!!" 
pln Отсюда видны развирающиеся на ветру флажки сказочного города. От него проложена широкая кирпичная дорога, которая, видимо, повидала немало ног на своем веку. Она уходит далеко в лес. Куда бы мне пойти?
btn glava1_vorota, В город
btn glava1_izby6ka, По тропинке глубже в лес
if count_glava1_les_znak=0 then btn glava1_les_znak, По дороге в неизвестность
if count_glava1_les_znak>0 then btn glava1_les_znak, По дороге к указателю
end

:glava1_vorota
pln 	Весь город окружен белокаменной стеной, уходящей далеко за пределы человеческого взгляда. На некотором расстоянии друг от друга, над стеной выступают макушки башен, весело махая флажками с грифончиком. На плоской крыше каждой из них, деловито перекидываясь в карты, охраняют покой мирных граждан благородные эльфийские лучники. Посреди стены находятся изящно выполненные золотые ворота, над которыми, блестя всеми цветами радуги, переливается надпись "Артамир".
btn glava1_vorota2, Подойти к воротам
btn Glava1_les, Вернуться на поляну
end

:glava1_vorota2
if inv_гражданство>0 then goto glava1_vorota3.3
if govorila_s_privratnikom=1 then goto glava1_vorota3
govorila_s_privratnikom=1
pln 	Когда я подошла к воротам, то заметила в них маленькое окошко на уровне глаз. Окошко открылось, и в нём что-то промелькнуло, крикнуло "привееет!" и упало вниз.
pln 	- Привет. - машинально ответила я. - Ты кто?
pln 	- Привратник! - снова выкрикнуло существо, лишь на мгновение задержавшись на уровне окна.
pln 	На этот раз я сумела разглядеть его лучше. Это животное напоминало маленькую собаку, только совершенно без шерсти. Чтобы ответить мне, привратник каждый раз подпрыгивал.
pln 	- Ты можешь открыть ворота? - без особой надежды спросила я. Существо было явно слишком маленьким, чтобы справиться с такими огромными вратами.
pln 	- Неет! - в очередной раз подпрыгнул он. И, видимо не очень ловко рассчитав прыжок, попал пастью прямо в окошко.
pln 	И судя по скрежету лап по металлу с другой стороны ворот - застрял намертво. С моей же стороны осталась лишь его пасть с черным носом, который постоянно двигался из стороны в сторону, что-то вынюхивая.
pln 	- Ну вот опять. И так каждый день. - грустно сказала пасть. - Теперь точно не смогу открыть тебе ворота. Давай, я расскажу как это сделать, а ты вытащишь меня отсюда? Только не ударом по носу как тот рыцарь! Освободишь меня когда сама попадешь в город. Договорились?
btn glava1_vorota2.1, - Договорились
end

:glava1_vorota2.1
pln 	- Договорились. - вздохнула я. - Думаю мне не составит труда тебя вытащить.
pln 	- Это хорошо! - успокоился привратник. - Тогда ищи эльфийское гражданство, девочка с зелёными глазами. Ворота почуют его, и сами отворятся.
btn glava1_vorota, - Я постараюсь не задерживаться. Держись!
end

:glava1_vorota3
pln 	Когда я подошла, черный нос нервно задергался, и привратник настороженно спросил:
pln 	- Кто здесь?! Уж не ты ли это, рыцарь Уевгсон? Знай же - страшная кара небес постигнет всякого, кто решиться прикоснуться к моему священному носу!
pln 	- Это всего лишь я. Алиса. - успокоила его я.
pln 	- Девочка с зелёными глазами... - вздохнул он. - Значит ты еще не достала гражданство.
btn glava1_vorota, - Уже скоро. Держись!
end

:glava1_vorota3.3
p 	Когда я приблизилась, ворота заскрипели, и начали нехотя открываться. 
if govorila_s_privratnikom=1 then goto glava1_vorota3.31
btn glava1_gorod, Войти в город
btn glava1_vorota5, Уйти в лес
end

glava1_vorota3.31
p Привратник смешно отъезжал в сторону вместе с одной из створок. 
if glvorota3per=0 then pln У него на спине я увидела слегка помятые, но довольно внушительные крылья! "Тогда почему он падал?" - задумалась я. & glvorota3per=1 else pln .
pln 	Наконец ворота остановились.
pln 	- Не забудь, ты обещала не оставлять меня здесь! - раздался испуганный голос, а за ним скрежет когтей по двери.
btn glava1_vorota4, Освободить привратника
btn glava1_gorod, Войти в город
btn glava1_vorota5, Уйти в лес
end

:glava1_vorota5
p 	Отходя я услышала за своей спиной скрип, - это закрывались золотые врата эльфийского города. 
if govorila_s_privratnikom=1 then pln 	Чуть позже к скрипу присоединяется горестный вздох "И так каждый день!" else pln 
goto glava1_vorota

:glava1_vorota4
govorila_s_privratnikom=2
pln 	Подойдя к этому странному существу, я осторожно вынула его пасть из окошка, и опустила привратника на землю. Меленький и легкий. Как кошка. Только лысый и серый.
pln 	- Фух, спасибо. Лучше поздно чем никогда. - проговорил он разминая лапы. - Правда я очень хорошо прыгаю? Никто так не умеет!
pln 	- Да, хорошо. - согласилась я, сравнив расстояние от земли до окошка с маленькими размерами крылатого существа. - А летать ты умеешь?
pln 	- Летать? - удивился он, смущенно оглянувшись на свои крылья. - А разве белки летают?
p 	Так вот оно что. Он ещё и белка. 
btn glava1_vorota4.4, - Летают, летают!
btn glava1_vorota4.5, - Ты не белка
end

:glava1_vorota4.5
pln - Ты не...  - начала было я, но сразу поняла что это бесполезно. Тут надо по-другому...
:glava1_vorota4.4
pln 	- Летают, летают! - заверила я его. - Еще как! Белки - самые лучшие летуны в мире! Тебе надо только подпрыгнуть, и быстро-быстро махать крыльями. Попробуй.
pln 	- Я же высоты еще боюсь. - неожиданно вспомнил привратник, но под моим суровым взглядом быстро сдался:
pln 	- Ладно. - решился он расправляя крылья и зажмурив глаза. - Была не была.
pln 	Пара взмахов, - и существо, кувыркаясь в воздухе, полетело в сторону леса.
btn glava1_gorod, Войти в город
btn glava1_vorota5, Уйти в лес
end

:glava1_gorod
pln 	Изнутри город сверкал ещё больше. Каждая крыша была украшена золотом, и драгоценными камнями. Но самое изумительное зрелище, - это дворец. Даже издалека были заметны огромные драгоценные камни на его башнях.
pln 	На встречу мне вышла красивая женщина в голубой мантии.
pln 	- Привет Алиса. Я ждала тебя. - удивительно спокойным, мягким голосом, произнесла она. - Могу ли я пригласить тебя в свой дворец? По пути можешь спросить меня обо всём.
pln 	Сама того не заметив я пошла следом за незнакомкой в сторону дворца.
pln 	- Кто вы? И зачем меня ждали? - растеряно спросила я. Откуда она узнала моё имя?
pln 	- Я, - Королева этого мира. Ждала для того чтобы проводить тебя домой... - неожиданно королева остановилась, и, обернувшись, положила руки мне на плечи. - Хотела бы я показать тебе дворец, но к сожалению уже не совсем осталось времени. - мягко произнесла она глядя в глаза, будто гипнотизируя. - Мы ещё увидимся, обещаю тебе. А сейчас пора возвращаться, Алиса. Уже утро.
btn Glava1_London2, - Что?
end















:glava1_izby6ka
pln 	Через несколько минут я вышла к ветхой деревянной избушке. Она стояла очень криво, опираясь на ближайшие деревья всем своим весом. 
btn glava1_izby6ka2, Постучать в дверь
btn glava1_cave, Идти дальше по тропинке
btn Glava1_les, Выйти на опушку леса
end

:glava1_izby6ka2
pln 	Я подошла, и постучалась в перекосившуюся деревянную дверь. Внутри домика что-то упало, раздался грохот, и испуганный тонкий голос сообщил что никого нет дома. Пожав плечами, я отошла от двери.
btn glava1_cave, Идти дальше по тропинке
btn Glava1_les, Выйти на опушку леса
end

:vopros1
vopros1=1
if count_vopros1=1 then pln 	Не успела я сделать и нескольких шагов, как передо мной, из-под земли, вырос огромный камень, загородив весь путь.
if count_vopros1<>1 then pln 	Камень всё стоит на том же месте, полностью перегородив дорогу.
pln 	"Камень вопроса". - прочитала я. - "Ответь на вопрос путник, и иди дальше с миром".
pln 	"Как зовут первого стражника, охраняющего мост на пограничье?".
btn vopros1_2, Ответить
btn glava1_izby6ka, Вернуться к домику
end

:vopros1_2
instr otvet=
input otvet
pln 	- #%otvet$! - выкрикнула я.
if otvet="уевгсон" or otvet="yevgson" or otvet="uevgson" then pln 	Камень разочарованно затрещал и скрылся под землей. & vopros1=2
if vopros1<>2 then pln 	Камень вопроса даже не шелохнулся.
if vopros1<>2 then btn vopros1_2, Ответить
if vopros1=2 then btn glava1_cave, Пройти дальше
btn glava1_izby6ka, Вернуться к домику
end

:glava1_cave
if vopros1<2 then goto vopros1
if inv_Бусы из жемчуга>0 and govorila_s_krolikom<>0 then goto glava1_cave1
pln 	Я нахожусь возле огромного входа в страшную пещеру, вход в которую выполнен в виде драконьей головы. Веет неприятным холодом. Неподалеку протекает река, блестя солнечными бликами, и создавая яркий контраст с мрачной пещерой.
btn glava1_cave2, Заглянуть в пещеру
btn glava1_re4ka, Подойти к речке
btn glava1_izby6ka, Вернуться к домику
end

:glava1_cave1
pln 	Неожиданно запахло горелой шерстью, и, из-за камня, выскочил уже знакомый мне кролик. Он выглядел немного опалённым, но это ничуть не уменьшило его болтливости:
pln 	- Привет. Знаешь, я шёл тут мимо и... Ух ты какие бусы! - запнулся Джеймс и как загипнотизированный уставился на жемчуг. Пришлось легонько стукнуть его по голове. - Кхм, о чём это я? Ах, ну да. В общем шёл я тут мимо, и подумал, не нужно ли тебе зеркальце? Из чистого золота! Меняю только на бусы!
btn glava1_cave1.1, - По рукам.
btn glava1_cave1.2, - Нет, спасибо.
end

:glava1_cave1.1
inv- Бусы из жемчуга
inv+ Зеркальце
pln 	- Ура! Теперь у меня есть бусы! На что бы их поменять... - думал вслух кролик скрываясь из виду.
btn glava1_cave2, Заглянуть в пещеру
btn glava1_re4ka, Подойти к речке
btn glava1_izby6ka, Вернуться к домику
end

:glava1_cave1.2
pln 	- Погоди, как?! - занервничал кролик. - Ты не можешь так поступить со мно.. то есть с Реной! Я совершенно случайно подслушал ваш разговор. Это самое красивое зеркало во всём королевстве, уверяю тебя! Оно было в сокровищнице дракона и когда то принадлежало принцессе Лине! Все остальные зеркала по сравнению с ним - жалкие подделки! Это будет достойным подарком, верно? Рена спасла тебе жизнь! Это самое меньшее, что ты можешь для неё сделать!
pln 	Мне стало стыдно.
btn glava1_cave1.1, - Думаю, ты прав.
end

:glava1_cave2
if zagadkifilina=0 then zagadkifilina=5
pln 	Я вошла в пещеру. Её тёмные своды грозно сморят на меня так, что под их нехорошим взглядом хочется спрятаться. Где-то в делеке, эхом отскакивая от стен, раздаётся тяжёлое дыхание.
pln 	Я заметила как углу, на останках человеческого скелета, сидит большой филин, спрятавшись в тенях.
if glava1_pti4ka_zagadka<>zagadkifilina then proc glava1_filinpre
if glava1_pti4ka_zagadka=zagadkifilina then btn glava1_cave3, Пройти дальше в пещеру
btn glava1_cave, Выйти из пещеры
end

:glava1_filinpre
pln 	- Ухх - проволчал он недовольно. - Человек! Отгадаешь загадку - получишь монетку!
proc glava1_filin#glava1_pti4ka_zagadka$

if vse_zagadki=1 then btn glava1_chiter, - Не старайся. Я знаю все загадки. else btn hzotvet, Ответить
end

:glava1_cave2.2
if glava1_pti4ka_zagadka<>zagadkifilina then proc glava1_filin#glava1_pti4ka_zagadka$&goto hzotvet
if glava1_pti4ka_zagadka=zagadkifilina then pln 	- Увы, загадок больше нет. - расстроился филин и уснул.& btn glava1_cave3, Пройти дальше в пещеру
btn glava1_cave, Выйти из пещеры
end

:glava1_filin0
pln 	- Маленький, розовый, кусается?
end
:glava1_filin1
pln 	- Большой, рыжий, много врёт?
end
:glava1_filin2
pln 	- Маленькая, рыжая, летает?
end
:glava1_filin3
pln 	- Большая, серебряная, наблюдает?
end
:glava1_filin4
pln 	- Маленький, зелёный, прыщавый?
end
:glava1_filin5
pln 	- Большой, железный, гремит?
end
:glava1_filin6
pln 	- Маленький, деревянный, стреляет?
end

:hzotvet
instr otvet=
input otvet
pln 	- #%otvet$? - предположила я.
if glava1_pti4ka_zagadka=0 then var1="krolik"&var2="кролик"
if glava1_pti4ka_zagadka=1 then var1="coyote"&var2="койот"
if glava1_pti4ka_zagadka=2 then var1="belka"&var2="белка"
if glava1_pti4ka_zagadka=3 then var1="luna"&var2="луна"
if glava1_pti4ka_zagadka=4 then var1="ogyrec"&var2="огурец"
if glava1_pti4ka_zagadka=5 then var1="capitan"&var2="капитан"
if glava1_pti4ka_zagadka=6 then var1="bow"&var2="лук"

if otvet=var1 or otvet=var2 then pln 	- Да. - одобрительно кивнул филин. Держи монетку.&inv+ Лунный блик&glava1_pti4ka_zagadka=glava1_pti4ka_zagadka+1&btn glava1_cave2.2, - Ещё загадку! else pln 	- Вот и не угадала! - обрадовался филин.&btn hzotvet, - Я хочу попробовать ещё раз!
btn glava1_cave, Выйти из пещеры
end




:glava1_chiter
glava1_pti4ka_zagadka=zagadkifilina
pln 	Филин несколько секунд внимательно смотрел на меня, затем разочарованно вздохнул: pln 	- Хорошо, проходи. - сказал он засыпая.
btn glava1_cave3, Пройти дальше в пещеру
btn glava1_cave, Выйти из пещеры
end

:glava1_cave3
pln 	Тяжёлое дыхание слышится всё ближе, и вскоре я замечаю табличку "Умнейший дракон во вселенной. Прямо по коридору третья дверь слева".
if count_glava1_cave3.2=0 then btn glava1_cave3.1, Идти к дракону else pln Но я к нему больше не пойду!
btn glava1_cave, Выйти из пещеры
end

:glava1_cave3.1
pln 	- Ого, посетители. - удивился дракон увидев меня.
pln 	- Алиса. - представилась я.
pln 	- Умнейший дракон во вселенной. - в свою очередь скромно назвался он, поправляя очки. - Ты можешь спросить у меня всё что тебе угодно. Я знаю всё.
if count_glava1_cave3.2=0 then btn glava1_cave3.2, - Как мне вернуться домой?
btn glava1_cave, - Извините, мне пора.
end

:glava1_cave3.2
pln 	- Как вернуться?.. - на секунду задумался он. - Понятия не имею. - наконец ответил дракон, и вернулся к чтению своей книги, потеряв ко мне весь интерес.
pln 	"Как разводить кактусы" - прочитала я на обложке.
pln 	Если это умнейший дракон, то пожалуй с глупейшим лучше не встречаться.
btn glava1_cave, - Ясно. Пока.
end

:glava1_re4ka
pln 	Я возле реки. Безмятежные черные воды. Ни единой волны. Думаю тут очень глубоко. Наверное, я бы искупалась, если бы умела плавать. Интересно, что на другом берегу? Река такая широкая, что отсюда не рассмотреть.
btn glava1_cave, Назад к пещере
if inv_Ракушка>0 then btn glava1_re4ka1, Бросить ракушку в реку
if count_glava1_re4ka2=0 then btn glava1_re4ka2, Переплыть!
end

:glava1_re4ka1
inv- Ракушка
if count_glava1_re4ka2>0 and otdala_zerkalo<>2 then goto glava1_re4ka1.1
pln 	Ракушка весело запрыгала по водной глади оставляя на воде круги.
btn glava1_cave, Назад к пещере
if inv_Ракушка>0 then btn glava1_re4ka1, Бросить ракушку в реку
if count_glava1_re4ka2=0 then btn glava1_re4ka2, Переплыть!
end

:glava1_re4ka1.1
pln 	Ракушка зарябила на воде, но её быстро поймала тонкая женская рука. Из воды показалась русалка.
if count_glava1_re4ka1.1>1 then goto glava1_re4ka1.2
pln 	- Алиса. - поклонилась я. - Пришла спасибо сказать. И за бусы тоже. Очень красивые.
pln 	- А я Рена. - рассмеялась русалка. - Я как смотрю у тебя всё наладилось? Топиться больше не собираешься?
pln 	- Нет. - смутилась я. - Больше не собираюсь.
pln 	- Вот и хорошо. - погрустнела Рена. - Да я уже и привыкла одна.. Эх не надо было тебя спасать. - совсем расстроилась она.
pln 	Я растерялась, не зная что сказать.
pln 	- Слушай, - вдруг осенило её. - принеси мне зеркальце из города! Я помню у меня было замечательное зеркальце когда я была... живой. Я могла смотреть в него часами... Ты ведь принесёшь? - моляще посмотрела на меня русалка. - Пообещай что принесёшь!
btn glava1_cave, - Обязательно принесу! Обещаю!
end

:glava1_re4ka1.2
pln 	- Привет. Ты ко мне с зеркальцем? - с надеждой спросила русалка.
if inv_Зеркальце>0 then btn glava1_re4ka1.3, - Ага! Держи.
btn glava1_re4ka1.22, - Ещё нет, извини.
end

:glava1_re4ka1.22
pln 	- Жаль... Я буду ждать, и верю что ты не забудешь! - грустно сказала русалка скрываясь в воде.
btn glava1_cave, К пещере.
end

:glava1_re4ka1.3
inv- Зеркальце
otdala_zerkalo=2
pln  - Какое оно красивое! - восхитилась Рена, не в силах оторвать взгляд от зеркала. - Спасибо тебе, оно просто прелестно!
pln 	- Алиса! - наконец опомнилась русалка, но в неё глазах ещё сияли блики отражённого солнца. - У меня для тебя тоже есть подарок! Подожди минутку.
pln 	Рена нырнула в воду, и через несколько мгновений появилась на поверхности, осторожно держа что-то маленькое руке.
pln 	- Это волшебная улитка. - показала она мне маленькое серое существо, разжав ладонь. - Она исполнит любое твоё желание.
pln 	- Волшебная улитка? - удивилась я.
pln 	- Ну золотые рыбки у нас в реке не водятся. - немного смутилась русалка. - Есть только волшебные улитки. Они не такие сильные, и не могут исполнить некоторые, слишком сложные, желания. Поэтому подумай прежде чем загадать. И помни что перезагадывать нельзя!
btn glava1_sea_case1, Я хочу.. домой.
btn glava1_sea_case2, Хочу миллион Лунных Бликов!
;btn glava1_sea_case3, Хочу знать все загадки!
btn glava1_sea_case4, Хочу получить эльфийское гражданство!
end

:glava1_sea_case1
pln 	Что-то вспыхнуло, и я на секунду потеряла сознание.
goto Glava1_London2
:glava1_sea_case2
inv+ 10, Лунный блик
goto glava1_sea_case_last
:glava1_sea_case3
vse_zagadki=1
goto glava1_sea_case_last
:glava1_sea_case4
inv+ Гражданство
goto glava1_sea_case_last


:glava1_sea_case_last
pln 	Что-то вспыхнуло, и я на секунду потеряла сознание. Русалки нигде не было.
btn glava1_re4ka, Осмотреться
end

:glava1_re4ka2
pln 	Я осторожно подошла к берегу, и, сняв обувь, решила проверить не слишком ли холодная река. Но тут моя нога соскользнула с мокрого камня, и я по макушку оказалась в черной, но удивительно чистой, воде, с каждой секундой погружаясь всё глубже...
pln 	Когда искорка сознание уже начала угасать, до меня донёсся тонкий голос:
pln 	- Умирать? Не слишком рано? - голос был женским и немного расстеряным. - Мне спасти тебя? Только кивни!
pln 	Но у меня не было уже никаких сил, каждый мускул был будто скован холодом.
pln 	- Ну пожалуйста, кивни!
pln 	К тому же... как кто-то может разговаривать под водой?
pln 	Раздалось недовольное ворчание, и я почувствовала, как чьи то руки подхватили меня и потащили наверх.
btn glava1_re4ka3, Очнуться
end

:glava1_re4ka3
inv+ Бусы из жемчуга
pln 	Я пришла в себя на берегу, дрожа от холода.
pln 	- Какие же вы люди хрупкие! - раздался звонкий смех. На том самом камне, с которого я подскользнулась, сидела самая настоящая русалка, поигрывая бусами из жемчуга. - Знаешь, а я тебе жизнь спасла!
pln 	Я попыталась её поблагодарить, но вместо слов раздался только кашель.
pln 	- Да ладно, я и так знаю что ты меня теперь ненавидишь. - смущенно продолжила русалка. - Плыву себе, никого не трогаю, и вдруг ты! Если честно я сначала не хотела тебя спасать. Так скучно одной.. да и благодарности от утопленников не дождёшься, но потом жалко тебя стало глупую.
pln 	Тем временем у меня получилось подняться. В глазах мигом потемнело от яркого солнца. Одежда уже почти высохла.
pln 	- Ну в общем я тоже тут когда-то утопилась. - прежний веселый тон сменила грусть - Парень меня бросил. Теперь жалею ведь.. Держи бусы на память.
pln 	Русалка швырнула великолепные бусы словно безделушку. Я не прилагая усилий поймала их, и жемчуг, переливаясь всеми цветами радуги, засиял в моей руке.
pln 	- Так что подумай хорошо ещё раз, и, если всё совсем плохо, приходи сюда снова. - подмигнула она. - Больше спасать не буду. Вместе веселей. Пока.
pln 	Махнув на прощание хвостом, русалка скрылась в воде.
btn glava1_re4ka, Осмотреться
end





















:glava1_les_znak
if kontrakt=1 and inv_лук>0 and govorila_s_kaetom=0 and govorila_s_krolikom<>2 then goto glava1_les_znak_kaet
if count_glava1_les_tropa=1 then pln 	Через некоторое время я наткнулась на развилку дорог с указательный знаком, и остановилась чтобы прочесть:
if count_glava1_les_tropa<>1 then pln 	Я возле указательного знака развилки. Вот что на нём написано:
pln 	"Юг - древний эльфийский город Артамир"
pln 	"Север - северный пограничный пост железного капитана"
pln 	"Запад - селение маленьких злобных зелёных человечков"
btn Glava1_les, Идти на юг (к городу)
btn glava1_tropa, Идти на север (к посту)
btn glava1_goblins, Идти на запад (в деревню карликов)
if govorila_s_kaetom<>0 then btn Glava1_les_derevo, Подойти к дереву койота
end

:glava1_goblins0
inv- 10,Лунный блик
govorila_s_gnomom=2
pln 	Карлик взял деньги и скрылся в норе.
:glava1_goblins
if govorila_s_gnomom<>2 then goto glava1_goblins_pre
pln 	Я в деревне маленьких зелёных человечков. Штук десять бегают вокруг моих ног, иногда покусывая кроссовки. И лишь один ведёт себя спокойно, и даже приветливо помахал рукой.
btn glava1_goblins1, - Привет.
btn Glava1_les_znak, Уйти
end

:glava1_goblins1
pln 	- Привет. - согласился он. - Я карлик-торговец. Продаю шерсть волка. Одна шерстинка - десять лунных бликов.
if inv_Лунный блик>=10 then btn glava1_goblins2, - По рукам!
btn glava1_goblins, - Нет, спасибо.
end

:glava1_goblins2
inv+ Шерсть волка
inv- 10, Лунный блик 
btn glava1_goblins, - Спасибо.
end

:glava1_goblins_pre
govorila_s_gnomom=1
pln 	Не успела я пройти и несколько ярдов, как на дорогу выскочил карлик в разбойничьей маске с вилкой.
pln 	- Просто так никто не пройдёт! - запищал он грозно размахивая оружием. - Заплатите пошлину в десять лунных бликов!
if inv_Лунный блик>=10 then btn glava1_goblins0, Заплатить
btn Glava1_les_znak, Уйти
end

:Glava1_les_derevo
pln 	Я возле большой сосны. Возле дерева стоит табличка с предупреждающей надписью "ОСТОРОЖНО: ХИТРЫЙ ЛАТЫШСКИЙ КОЙОТ" и стрелка, указывающая наверх.
pln С дерева свисает верёвка с колокольчиком. Наверное это звонок.
if govorila_s_krolikom<>2 and govorila_s_gnomom<>2 then btn Glava1_les_derevo2, Позвонить else btn Glava1_les_derevo3, Позвонить
btn Glava1_les_znak, Уйти
end

:Glava1_les_derevo2
pln 	Я дёрнула за верёвку, колокольчик весело зазвенел. С дерева незамедлительно упал койот.
pln 	- Привет, давно не виделись - убито сказал он поднимаясь с земли.
pln 	- Очень больно падать с такой высоты оказывается. Не надо было привязывать верёвку к ноге. Ничего, исправлю в следующей версии. - размышлял он, совершенно забыв про меня. 
if govorila_s_gnomom=1 then btn Glava1_les_derevo2.3, Спросить про гнома-разбойника
btn Glava1_les_znak, Уйти
end

:Glava1_les_derevo3
pln 	Я дёрнула за верёвку, колокольчик весело зазвенел. Койота нигде нет.
btn Glava1_les_znak, Уйти
end

:Glava1_les_derevo2.3
govorila_s_gnomom=2
pln 	- Гном говоришь? Без доспехов, точно? - хитро сощурился койот облизываясь. - Недалеко отсюда значит.. Знаешь, я тут вспомнил, - у меня срочные дела! Пока!
pln 	Койот надел плащ, очки, и жонглируя мандаринами, направился на запад, насвистывая какую-то мелодию.
pln 	Кажется он подозрительно себя ведёт.
btn Glava1_les_znak, Уйти
end

:glava1_les_znak_kaet
govorila_s_kaetom=1
pln 	Перед знаком сидел, чеша затылок, рыжий волк, видимо, пытаясь прочесть надпись на указателе. Судя по всему, получалось у него это не очень.
pln 	Я осторожно достала лук и прицелилась.
pln 	- Аааа!! Не стреляй!!! - зажмурившись, и смешно махая лапами завопил волк, заметив меня.
pln 	Пока я раздумывала что мне делать, волк осторожно приоткрыл один глаз:
pln 	- Не будешь, правда? А то тут все целый день ходят, стреляют...
pln 	- Не буду. - вздохнула я опуская лук. - Но мне очень нужна шерсть волка. Ну, может ты добровольно...
pln 	- Нет, ты что! - снова завопил он - У меня жена, дети. И на самом деле я вовсе не волк! Я койот. Точно, койот. Тебе же не нужна шерсть койота? Вот и правильно, что с нас, койотов, взять. Скажу тебе по секрету: у волков шерсть гораздо лучше...
pln 	- Кстати, у тебя нет ничего поесть? - поинтересовался он, обнюхивая мою ногу. - А то с утра голодный как волк.
pln 	- Эй! - воскликнула я, приподнимая лук. - Меня нельзя есть.
pln 	- Понял! Вопросов больше нет! - испугался он. - Хотя нет, есть один. 
pln 	Койот настороженно оглянулся по сторонам, и протянув пасть к самому моему уху, шепотом спросил:
pln 	- Как написать свой контрол вэб-браузера на вижуал бэйсике шесть?
pln 	- Эээ, не знаю. - удивилась я. Какой-то странный койот.
pln 	- Я так и знал! - в который раз заскулил он. - Никто не знает. Только Уевгсон, но он издевается и не говорит. Я сейчас запою с горя. Ыыыыы. - протяжно завыл койот, отстукивая такт задней лапой. - Ыыыыыыыы.
btn glava1_les_znak_kaet1, - Водятся ли здесь волки?
btn glava1_les_znak_kaet2, - Мне пора идти.
end

:glava1_les_znak_kaet1
pln 	- Тссс, говори шепотом. - настороженно оглядываясь прошептал он. - Волк один есть, но это страшная тайна. Никто не знает кто он... Но я бы посоветовал тебе присмотреться к Уолксу. - говоривший шепотом койот выглядел комично. - Мне кажется, он и есть волк.
btn glava1_les_znak_kaet2, - Спасибо. Мне пора идти.
end

:glava1_les_znak_kaet2
pln 	- Хочешь, приглашу тебя к себе в гости? Я живу вооон на том дереве.
pln 	Я недоверчиво осмотрела огромную сосну, на которую койот указывал пальцем. Её верхушка уходила далеко в небо.
pln 	- Нет спасибо, в другой раз. - стараясь быть вежливой, ответила я.
pln 	- Я так и знал! - довольно запрыгал он. - Только койоты достаточно ловки чтобы лазить по таким высоким деревьям! Смотри!
pln 	Подойдя к сосне, это странное существо начало неуклюже карабкаться на дерево со скоростью смертельно раненой улитки.
btn glava1_les_znak, Оставить сумасшедшего койота в покое
end

:glava1_tropa
if inv_лук>0 and govorila_s_krolikom=0 then goto glava1_krolik
pln 	Я у границы. Это огромный мост через реку, весь берег которой усыпал ракушками. На мосту скучающе стоят два солдата. Неподалеку, гордо помахивая флажком с грифоном, стоит одинокая сторожевая башня. Вблизи моста стоит перекосившийся знак на котором корявым почерком написано следуещее: "Монстры страшные на земле стороны темной водятся. Осторожны будьте поэтому". На юго-запад отсюда уходит небольшая тропинка.
btn glava1_les_znak, Вернуться к указателю
btn glava1_town, Зайти в башню
if count_glava1_orcland=0 then btn glava1_orcland, Перейти мост
btn glava1_tropa_sl, Поговорить со стражей
btn glava1_tropa_sl_rakushka, Подобрать ракушку
end

:glava1_tropa_sl_rakushka
if inv_Ракушка>0 then pln 	- У меня уже есть одна! else pln 	Я выбираю самую красивую ракушку и беру её с собой.&inv+ Ракушка
btn glava1_tropa, Осмотреться
end






; Кролик. Джеймс Кролик.

:glava1_krolik
govorila_s_krolikom=1
pln 	Едва я вышла из сторожевой башни, как вдруг, передо мной, из кустов, выскочил небольшой розовый заяц.
pln 	- Кролик. - улыбнулся он, обнажая четыре ряда белоснежных зубов, в острых кончиках которых отражались солнечные блики. - Кролик Джеймс Соник Сумкин. - представился он полностью.
pln 	- Очень приятно, Алиса. - не смотря на улыбку, а может именно из-за неё, Кролик выглядит страшно. Кажется он задумал недоброе.
pln 	- Кажется я задумал недоброе. - шепотом сообщил он. - Продай мне свой лук. Только сейчас даю тебе за него восемь бликов луны!
btn glava1_krolik_1, - Бери так, я не воин.
btn glava1_krolik_2, - Продано.
btn glava1_krolik_3, - Не могу, он не мой.
end

:glava1_krolik_2
inv+ 8, Лунный блик

:glava1_krolik_1
if count_glava1_krolik_2=0 then zagadkifilina=7
govorila_s_krolikom=2
inv- Колчан стрел
inv- Лук
pln 	 - Спасибо добрая девочка! Теперь я покажу этому койоту! - радостно завопил кролик, и скрылся в лесу.
pln Ох, не к добру это.
btn glava1_tropa, Осмотреться
end

:glava1_krolik_3
pln 	 - Как жаль! Мой дьявольский план провалился! - разочарованно воскликнул кролик, и скрылся в лесу.
btn glava1_tropa, Осмотреться
end














:glava1_town
if kontrakt=2 then goto glava1_town2.3
if count_glava1_town<>1 and kontrakt=0 then goto glava1_town2
if count_glava1_town<>1 and kontrakt=1 then goto glava1_town2.1
pln 	- Привет! - воскликнула невысокая фигура в серебристых латах, едва я успела переступить порог, и лязгая доспехами подбежала ко мне. - Не хочешь в наемники?
pln 	- Вы возьмете в наемники меня? - искренне удивилась я.
pln 	И секундой позже решила поиздеваться:
pln 	- Вас не будет мучить совесть если меня убьют?
pln 	- Будет! Я даже заплачу! Вот! - вздохнула груда железа.
pln 	- Не надо, заржавеете.. - осторожно начала я, но он меня перебил:
pln 	- Решайся девочка! Сегодня у нас всем записавшимся в стражу бесплатная выдача оружия, доспехов, а также удвоенная оплата и эльфийское гражданство...
if govorila_s_privratnikom=1 then btn glava1_town3, Записаться в наемники
btn glava1_tropa, Тихонько смыться
end

:glava1_town2
pln 	- А, опять ты девочка! Дай угадаю - ты передумала и решила получить значок наемника? - запрыгал железный капитан. - Это замечательно! Ты не представляешь какие славные бонусы ждут благородного солдата великой эльфийской армии! Мы даже предоставляем бесплатное вооружение...
if govorila_s_privratnikom=1 then btn glava1_town3, Записаться в наемники
btn glava1_tropa, Тихонько смыться
end

:glava1_town2.1
pln 	- Вольно солдат! Как продвигается задание? - заскрипел капитан.
if inv_шерсть волка>0 then btn glava1_town2.2, - Я принесла шерсть волка!
btn glava1_tropa, - Всё плохо. Я позже зайду.
end

:glava1_town2.2
inv- Шерсть волка
inv+ 5,Лунный блик
inv+ Гражданство
kontrakt=2
p 	- Отлично, можешь идти пропивать жалование солдат! - сообщил капитан. 
:glava1_town2.3
pln 	- Новых заданий пока нет.
btn glava1_tropa, Выйти из башни.
end

:glava1_town3
inv+ Значок наемника
inv+ Лук
inv+ Колчан стрел
pln 	- Уверяю тебя что ты не пожалеешь что вступила в доблестную эльфийскую армию! - сказал капитан изо всех сил тряся меня за руку. - Поставь крестик здесь, здесь, и здесь...
pln 	От доспехов я отказалась. Взяла только приглянувшийся мне легкий лук, хотя капитан очень убеждал меня взять булаву, которую я не могла поднять. И очень расстроился когда я отказалась.
btn glava1_town3_1, - Какое мое первое задание?
if govorila_s_privratnikom=1 then btn glava1_town3_2, - Когда мне дадут эльфийское гражданство?
end

:glava1_town3_1
kontrakt=1
inv+ 3,Лунный блик
pln 	- У нас в лесу появился волк! И его никто никогда не видел! Это очень редкое существо... - капитан замялся, и даже на какую-то долю секунды перестал скрипеть своими несмазанными доспехами. - Я слышал что шерсть волка исцеляет от любых болезней.
pln 	- Вы чем то болеете? - невинно поинтересовалась я.
pln 	- Да. Нет. Неважно. - смущенно дал он точный ответ. - В общем твое первое задание состоит в том, чтобы принести мне шерсть волка.
pln 	- Мне жалко убивать волка. - подумав сказала я. - Я не смогу.
pln 	- Нет, нет! Убивать никого не надо! Мне нужна пара шерстинок и всё! - испугался капитан. - Не вздумай его убивать. Он у нас один.
pln	Какая я кровожадная оказывается.
pln 	- Вот твоё жалование за неделю вперед. - успокоился он. - Постарайся истратить их так, чтобы на мою башню потом не показывали пальцами, как это было с Уевгсоном...
if count_glava1_town3_2=0 and govorila_s_privratnikom=1 then btn glava1_town3_2, - Когда мне дадут эльфийское гражданство?
btn glava1_tropa, Выйти из башни
end

:glava1_town3_2
pln 	- После выполнения первого задания разумеется! Ты получишь настоящее эльфиское гражданство и премию!
if kontrakt=0 then btn glava1_town3_1, - Какое мое первое задание?
if kontrakt=1 then btn glava1_tropa, Выйти из башни
end
















:glava1_orcland
pln 	- Извини, - остановил тебя первый стражник. - мы доблестные эльфийские воины не можем тебя пропустить на ту сторону моста! Наш долг смотреть за тем чтобы никто не прошёл по нему! Если понадобиться мы будем смело сражаться и возможно даже погибнем в неравной схватке с превосходящими силами противника, но и они понесут немалые потери!..
btn glava1_tropa, Вернуться
btn glava1_town, Зайти в башню
end

:glava1_tropa_sl
if count_glava1_tropa_sl<>1 then goto glava1_tropa_sl3
pln 	Подойдя ближе я поняла что стража тут явно для виду...
pln 	- Привет. - обратилась я к ним. - Охраняете покой честных граждан?
pln 	- Ага. - обрадовался первый - Точно. Охраняем.
pln 	- Хм? - удивился второй.
btn glava1_tropa_sl2, - И часто нападения бывают?
btn glava1_tropa, - Ясно.
end

:glava1_tropa_sl2
pln 	- Ага. - сказал первый - Нападения каждый день. Но мы их яростно отбиваем, неся огромные потери.
pln 	- Хм. - подтвердил второй.
pln 	- Ага, я и говорю. А горы трупов сбрасываем в реку. - подмигнул куда более болтливый первый страж.
pln 	Хвастуны.
btn glava1_tropa, - Ясно.
end

:glava1_tropa_sl3
if count_glava1_tropa_sl3<>1 and count_glava1_orcland>0 then goto glava1_tropa_sl4
pln 	- Ага, это снова ты! Привет. Хочешь мы научим тебя сверхсекретной, беспроигрышной, эльфийской технике ведения боя против семерых противников? Мы правда ей владеем! Уолкс, скажи! - первый страж толкает напарника.
pln 	- Хмм? - проснулся Уолкс.
pln 	- Ничего, спи дальше. - ответил он, и уже обращаясь ко мне продолжил:
pln   - Только вся проблема в том что противников должно быть именно семь - не больше и не меньше! В общем ставишь одного врага в центр...
btn glava1_tropa, - Ээ, спасибо не надо!
end

:glava1_tropa_sl4
pln 	На посту сейчас только Уолкс. Первого стражника нигде нет.
if count_glava1_tropa_sl5=0 then btn glava1_tropa_sl5, - Уолкс, а где твой напарник?
btn glava1_tropa, Уйти.
end

:glava1_tropa_sl5
pln 	Уолкс удивленно осмотрелся вокруг.
pln 	- Хм. - наконец пожал плечами он.
btn glava1_tropa, - Ясно.
end







:Glava1_London2
invkill
pln 	- Алиса, проснись милая. - повторил знакомый голос. - Тебе пора в школу. Снилось что-то интересное?
pln 	Русалка Рена, Королева, весёлый стражник Уевгсон, - все они ещё стояли перед глазами, но с каждой секундой вспомнить их лица становилось всё сложнее.
pln 	- Да.. или нет.. Я не помню...
btn exit, Конец
end

;========================================use========================================

:use_Лунный блик
pln 	- Собранные могучим магом, в полнолуние, только мы, серебряные блики истинного света, обладаем силой самой луны... - тихонько шепчут лунные блики всякую чушь.
end

;(с) Sylar 2008 г.
;Посвящается одному очень хорошему человеку, которого тоже зовут Алиса.